plugins {
    id "com.diffplug.gradle.spotless" version "3.26.1"
}
apply plugin: 'com.android.application'

android {
    compileSdkVersion 32
    defaultConfig {
        applicationId "com.github.gotify"
        minSdkVersion 19
        targetSdkVersion 32
        versionCode 25
        versionName "2.3.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        signingConfig signingConfigs.debug
        resValue "string", "app_name", "Gotify"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        development {
            applicationIdSuffix ".dev"
            debuggable true
            resValue "string", "app_name", "Gotify DEV"
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
        lintConfig file('../lint.xml')
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }
}

if (project.hasProperty('sign')) {
    android {
        signingConfigs {
            release {
                storeFile file(System.getenv("RELEASE_STORE_FILE"))
                storePassword System.getenv("RELEASE_STORE_PASSWORD")
                keyAlias System.getenv("RELEASE_KEY_ALIAS")
                keyPassword System.getenv("RELEASE_KEY_PASSWORD")
            }
        }
    }
    android.buildTypes.release.signingConfig android.signingConfigs.release
}

dependencies {
    implementation project(':client')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.preference:preference:1.2.0'

    implementation 'com.jakewharton:butterknife:10.2.3'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.3'
    implementation 'com.hypertrack:hyperlog:0.0.10'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'io.noties.markwon:core:4.6.2'
    implementation 'io.noties.markwon:image-picasso:4.6.2'
    implementation 'io.noties.markwon:image:4.6.2'
    implementation 'io.noties.markwon:ext-tables:4.6.2'
    implementation 'io.noties.markwon:ext-strikethrough:4.6.2'
    implementation 'com.squareup.retrofit2:retrofit:2.0.2'
    implementation 'org.threeten:threetenbp:0.7.2'
    implementation "androidx.multidex:multidex:2.0.1"
    implementation 'androidx.work:work-runtime:2.7.1'
}

configurations {
    all {
        exclude group: 'org.json', module: 'json'
    }
}

spotless {
    java {
        target '**/*.java'
        googleJavaFormat().aosp()
        removeUnusedImports()
        importOrder('', 'static *')
    }
}
